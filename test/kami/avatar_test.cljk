(ns kami.avatar-test
  (:require [clojure.test :refer [deftest is]]
            [kami.avatar :as avatar]))

(deftest reduced-avatar-quality-sweep
  (let [avatars (mapv (fn [n] {:avatar/id (keyword (str "avatar-" n))
                               :avatar/position [(* n 5) 0 0]}) (range 8))
        sweep (avatar/avatar-sweep avatars [0 0 0]
                                   {:hero-distance 10 :animated-distance 25
                                    :impostor-distance 40}
                                   {:hero 1 :animated 2 :impostor 3})]
    (is (= [:hero :animated :animated :impostor :impostor :impostor :hidden :hidden]
           (mapv :avatar/quality sweep)))
    (is (= (set (map :avatar/id avatars)) (set (map :avatar/id sweep))))))

(deftest rtc-state-is-ordered-and-idempotent
  (let [room (-> (avatar/rtc-room)
                 (avatar/update-peer {:peer/id :a :peer/revision 0 :peer/state :present})
                 (avatar/update-peer {:peer/id :a :peer/revision 1 :peer/state :negotiating})
                 (avatar/update-peer {:peer/id :a :peer/revision 2 :peer/state :connected})
                 (avatar/update-peer {:peer/id :b :peer/revision 0 :peer/state :present}))
        duplicate (avatar/update-peer room {:peer/id :a :peer/revision 2
                                            :peer/state :degraded})]
    (is (= [:a] (avatar/connected-peers room)))
    (is (= room duplicate))
    (is (= 4 (:rtc/revision room))))
  (is (thrown? #?(:clj clojure.lang.ExceptionInfo :cljs js/Error)
               (avatar/update-peer (avatar/rtc-room)
                                   {:peer/id :a :peer/revision 0 :peer/state :connected}))))
